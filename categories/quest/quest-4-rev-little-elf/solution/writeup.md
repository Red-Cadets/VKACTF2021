## Терминальный доступ

| Событие | Название | Категория | Сложность |
| :------ | ---- | ---- | ---- |
| VKACTF 2021 | Терминальный доступ | Reverse | medium |

### Описание

> Автор: Rex
>
>Алиса уже хотела вылетать на Плутон, но в космопорте №1 её ждала неприятность, доступ к кораблю запретили, а для отлёта с Марса, как выяснилось, нужно специальное разрешение, которое не так-то просто получить. Космос Х на Марсе приветлив и хорошо встречает новых людей, но вот он не очень любит, когда люди, только прибыв, сразу улетают дальше. Местные службы направили Алису к пользовательскому терминалу, где с ней начал общаться бот, просивший маленький ELF. Алиса задумалась, осталось понять, что можно подать на вход и как обойти все проверки. Сможет ли она? Поиски отца необходимо продолжить! @LittleELF_Bot

### Решение
Нам необходимо собрать ELF размером меньше 150 байт, который будет читать флаг из flag.txt и выводить его. Вот пример такого ельфа:

```assembly
.set  ofs, 0x10000
.byte  0x7F
.ascii "ELF"
.long  0, 0, 0
.word  2
.word  3
.long  0 
.long  _start + ofs 
.long  phdr 
.long  0 
.long  0
.word  0 
.word  phdrsize 
.word  1 
.word  0 
.word  0 
.word  0

phdr:
    .long  1            
    .long  0            
    .long  ofs           
    .long  0            
    .long  filesize        
    .long  filesize        
    .long  7            */\* pflags = 7 (RWX) \*/*
    .long  0            
.set phdrsize, . - phdr
_start:
    movl  $5, %eax
    movl  $(str + ofs), %ebx
    movl  $0, %ecx
    movl  $777, %edx
    int   $0x80

    movl  %eax, %ebx
    movl  $3, %eax
    movl  $(flag + ofs), %ecx
    movl  $26, %edx
    int   $0x80

    movb  $4, %al
    movb  $1, %bl
    movl  $(flag+ofs), %ecx
    movb  $15, %dl
    int   $0x80

str:
    .ascii "flag.txt"
flag:
    .byte  0
.set filesize, .
```

После отправки файла получаем сообщение


Твой флаг:
**vka{h0bb17_3lf}**